///@category Backgrounds
///@param {array} layer			The array holding the data for the background layer
/*
Updates the data for a given background (or foreground) layer array.
These arrays are generated by <background_define> or <background_define_script>.

Please note: This script is automatically called by <obj_stage_manager>.
*/
function background_layer_update()
	{
	var _layer = argument[0];
	if (_layer[@ BACK.sprite] != -1)
		{
		if (_layer[@ BACK.xspd] != 0 || _layer[@ BACK.yspd] != 0)
			{
			_layer[@ BACK.x] += _layer[@ BACK.xspd];
			_layer[@ BACK.y] += _layer[@ BACK.yspd];
		
			//Wrapping around
			if (_layer[@ BACK.xspd] > 0 && _layer[@ BACK.x] > room_width)
				{
				_layer[@ BACK.x] -= room_width + (sprite_get_width(_layer[@ BACK.sprite]) * _layer[@ BACK.scale]);
				}
			if (_layer[@ BACK.xspd] < 0 && _layer[@ BACK.x] < 0)
				{
				_layer[@ BACK.x] += room_width + (sprite_get_width(_layer[@ BACK.sprite]) * _layer[@ BACK.scale]);
				}
			if (_layer[@ BACK.yspd] > 0 && _layer[@ BACK.y] > room_height)
				{
				_layer[@ BACK.y] -= room_height + (sprite_get_height(_layer[@ BACK.sprite]) * _layer[@ BACK.scale]);
				}
			if (_layer[@ BACK.yspd] < 0 && _layer[@ BACK.y] < 0)
				{
				_layer[@ BACK.y] += room_height + (sprite_get_height(_layer[@ BACK.sprite]) * _layer[@ BACK.scale]);
				}
			}
		
		var _num = sprite_get_number(_layer[@ BACK.sprite]);
		if (_num > 0)
			{
			_layer[@ BACK.frame] += _layer[@ BACK.imgspeed];
			if (_layer[@ BACK.frame] >= _num)
				{
				_layer[@ BACK.frame] -= _num;
				}
			}
		}
	}
/* Copyright 2024 Springroll Games / Yosi */