function online_css_ui_player_window_draw()
	{
	var _player_id = obj_online_css_ui.local_player_id;
	var _number = obj_online_css_ui.local_player_number;
	switch (state)
		{
		case CSS_PLAYER_WINDOW_STATE.select_character:
			//Surface
			if (!surface_exists(surf))
				{
				surf = surface_create(half_width * 2, half_height * 2);
				}
				
			surface_set_target(surf);
			draw_clear_alpha(c_white, 0);
			
			//Draw the character
			var _char = css_player_get(_player_id, CSS_PLAYER.character);
			var _color = css_player_get(_player_id, CSS_PLAYER.color);
			if (!setting().disable_shaders)
				{
				palette_shader_simple_set
					(
					character_data_get(_char, CHARACTER_DATA.palette_column_arrays)[@ 0], 
					character_data_get(_char, CHARACTER_DATA.palette_column_arrays)[@ _color],
					);
				draw_sprite_ext(character_data_get(_char, CHARACTER_DATA.css_portrait), 0, half_width, half_height, css_character_sprite_scale, css_character_sprite_scale, 0, c_white, 1);
				shader_reset();
				}
			else
				{
				var _blend = palette_color_get(character_data_get(_char, CHARACTER_DATA.palette_data), 0, _color);
				draw_sprite_ext(character_data_get(_char, CHARACTER_DATA.css_portrait), 0, half_width, half_height, css_character_sprite_scale, css_character_sprite_scale, 0, _blend, 1);
				}
				
			surface_reset_target();
			draw_surface(surf, x - half_width, y - half_height);
			break;
		case CSS_PLAYER_WINDOW_STATE.select_profile:
			//Draw background
			var _c = $666666;
			draw_rectangle_color(x - half_width, y - half_height, x + half_width - 1, y + half_height - 1, _c, _c, _c, _c, false);
			
			//Draw profiles
			draw_set_halign(fa_left);
			draw_set_valign(fa_middle);
			draw_set_color(c_white);
			draw_set_font(fnt_consolas);
			var _pad = half_height div 2;
			var _half = _pad div 2;
			var _c = c_dkgray;
			for (var i = 0; i < min(profile_count(), 4); i++)
				{
				var _num = profile_scroll + i;
				if (profile_current == _num) then draw_rectangle_color(x - half_width, y - half_height + (_pad * i), x + half_width - 1, y - half_height + (_pad - 1) + (_pad * i), _c, _c, _c, _c, false);
				draw_text(x - half_width + 28, y - half_height + _half + (_pad * i), profile_get(_num, PROFILE.name));
				if (profile_get(_num, PROFILE.autogenerated))
					{
					draw_sprite(spr_icon_profile_auto, 0, x - half_width, y - half_height + _half + (_pad * i));
					}
				}
			break;
		}
	}
/* Copyright 2024 Springroll Games / Yosi */